ext {
    preDexLibs = !project.hasProperty('disablePreDex')

    version = '1.4.0'
    versionCode = 26
    minSdkVersion = 9
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.3'

    supportLibraryVersion = '25.3.1'
    glideVersion = '3.8.0'

    deps = [
            supportAppCompat: "com.android.support:appcompat-v7:$supportLibraryVersion",
            supportDesign   : "com.android.support:design:$supportLibraryVersion",

            glide           : "com.github.bumptech.glide:glide:$glideVersion",
    ]
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
    }
}

allprojects {
    version = rootProject.ext.version

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    def appPlugin = "com.android.build.gradle.AppPlugin"
    def libraryPlugin = "com.android.build.gradle.LibraryPlugin"

    project.plugins.whenPluginAdded { plugin ->
        if (appPlugin == plugin.class.name || libraryPlugin == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
